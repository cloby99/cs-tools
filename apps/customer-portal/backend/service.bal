// Copyright (c) 2026 WSO2 LLC. (https://www.wso2.com).
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

import customer_portal.ai_chat_agent;
import customer_portal.authorization;
import customer_portal.entity;
import customer_portal.scim;
import customer_portal.types;
import customer_portal.updates;
import customer_portal.user_management;

import ballerina/cache;
import ballerina/http;
import ballerina/log;

final cache:Cache userCache = new ({
    capacity: 500,
    defaultMaxAge: 3600,
    evictionFactor: 0.2,
    cleanupInterval: 1800
});

service class ErrorInterceptor {
    *http:ResponseErrorInterceptor;

    # Intercepts the response error.
    #
    # + err - The error occurred during request processing
    # + return - Bad request response or error
    remote function interceptResponseError(error err) returns http:BadRequest|error {

        // Handle data-binding errors.
        if err is http:PayloadBindingError {
            string customError = "Payload binding failed!";
            log:printError(customError, err);
            return {
                body: {
                    message: customError
                }
            };
        }
        return err;
    }
}

// TODO: Remove after the ballerina header configs setting through choreo issue is fixed
http:ListenerConfiguration listenerConf = {
    requestLimits: {
        maxHeaderSize: 16384
    }
};

@display {
    label: "Customer Portal",
    id: "cs/customer-portal"
}
service http:InterceptableService / on new http:Listener(9090, listenerConf) {
    public function createInterceptors() returns http:Interceptor[] =>
        [new authorization:JwtInterceptor(), new ErrorInterceptor()];

    # Service init function.
    #
    # + return - Error if initialization fails
    function init() returns error? {
        log:printInfo("Customer Portal backend started.");
    }

    # Fetch user information of the logged in user.
    #
    # + return - User info object or error response
    resource function get users/me(http:RequestContext ctx)
        returns types:User|http:Unauthorized|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        string cacheKey = string `${userInfo.email}:userinfo`;
        if userCache.hasKey(cacheKey) {
            types:User|error cachedUser = userCache.get(cacheKey).ensureType();
            if cachedUser is types:User {
                return cachedUser;
            }
            log:printWarn(string `Unable to read cached user info for ${userInfo.email}`);
        }

        entity:UserResponse|error userDetails = entity:getUserBasicInfo(userInfo.email, userInfo.idToken);
        if userDetails is error {
            if getStatusCode(userDetails) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `Access denied for user: ${userInfo.userId} to Customer Portal`);
                return <http:Unauthorized>{
                    body: {
                        message: "Unauthorized access to the customer portal."
                    }
                };
            }

            string customError = "Failed to retrieve user data.";
            log:printError(customError, userDetails);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        string? phoneNumber = ();
        scim:User[]|error userResults = scim:searchUsers(userInfo.email);
        if userResults is error {
            // Log the error and return nil
            log:printError("Error retrieving user phone number from scim service", userResults);
        } else {
            if userResults.length() == 0 {
                log:printError(string `No user found while searching phone number for user: ${userInfo.userId}`);
            } else {
                phoneNumber = scim:processPhoneNumber(userResults[0]);
            }
        }

        types:User user = {
            id: userDetails.id,
            email: userDetails.email,
            firstName: userDetails.firstName,
            lastName: userDetails.lastName,
            timeZone: userDetails.timeZone,
            phoneNumber
        };

        error? cacheError = userCache.put(cacheKey, user);
        if cacheError is error {
            log:printWarn("Error writing user information to cache", cacheError);
        }
        return user;
    }

    # Update user information of the logged in user.
    #
    # + payload - User update payload
    # + return - Updated user object or error response
    resource function patch users/me(http:RequestContext ctx, types:UserUpdatePayload payload)
        returns types:UpdatedUser|http:BadRequest|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        if payload.keys().length() == 0 {
            return <http:BadRequest>{
                body: {
                    message: "At least one field must be provided for update!"
                }
            };
        }

        types:UpdatedUser updatedUserResponse = {};

        if payload.phoneNumber is string {
            scim:Phone phoneNumber = {mobile: payload.phoneNumber};
            scim:User|error updatedUser = scim:updateUser({phoneNumber}, userInfo.email, userInfo.userId);
            if updatedUser is error {
                if getStatusCode(updatedUser) == http:STATUS_BAD_REQUEST {
                    return <http:BadRequest>{
                        body: {
                            message: extractErrorMessage(updatedUser)
                        }
                    };
                }

                string customError = "Failed to update phone number.";
                log:printError(customError, updatedUser);
                return <http:InternalServerError>{
                    body: {
                        message: customError
                    }
                };
            }

            error? cacheInvalidate = userCache.invalidate(string `${userInfo.email}:userinfo`);
            if cacheInvalidate is error {
                log:printWarn("Error invalidating user information from cache", cacheInvalidate);
            }
            updatedUserResponse.phoneNumber = scim:processPhoneNumber(updatedUser);
        }

        if payload.timeZone is string {
            // TODO: Update timezone
        }

        return updatedUserResponse;
    }

    # Search projects of the logged-in user.
    #
    # + payload - Project search payload
    # + return - Projects list or error response
    resource function post projects/search(http:RequestContext ctx, entity:ProjectSearchPayload payload)
        returns http:Ok|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:ProjectsResponse|error projectsList = entity:searchProjects(userInfo.idToken, payload);
        if projectsList is error {
            if getStatusCode(projectsList) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(projectsList) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to requested projects are forbidden for user: ${userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            string customError = "Failed to retrieve project list.";
            log:printError(customError, projectsList);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return <http:Ok>{
            body: projectsList
        };
    }

    # Get project details by ID.
    #
    # + id - ID of the project
    # + return - Project details or error response
    resource function get projects/[string id](http:RequestContext ctx)
        returns entity:ProjectResponse|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:ProjectResponse|error projectResponse = entity:getProject(userInfo.idToken, id);
        if projectResponse is error {
            if getStatusCode(projectResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(projectResponse) == http:STATUS_FORBIDDEN {
                logForbiddenProjectAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            string customError = "Failed to retrieve project details.";
            log:printError(customError, projectResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return projectResponse;
    }

    # Get deployments of a project by ID.
    #
    # + id - ID of the project
    # + return - Deployments response or error response
    resource function get projects/[string id]/deployments(http:RequestContext ctx)
        returns types:Deployment[]|http:BadRequest|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:DeploymentsResponse|error deploymentsResponse = entity:getDeployments(userInfo.idToken, id);
        if deploymentsResponse is error {
            if getStatusCode(deploymentsResponse) == http:STATUS_FORBIDDEN {
                logForbiddenProjectAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            string customError = "Failed to retrieve deployments for the project.";
            log:printError(customError, deploymentsResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return mapDeployments(deploymentsResponse);
    }

    # Create a new deployment for a project.
    #
    # + id - ID of the project
    # + payload - Deployment creation payload
    # + return - Created deployment or error response
    resource function post projects/[string id]/deployments(http:RequestContext ctx,
            types:DeploymentCreatePayload payload)
        returns entity:CreatedDeployment|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:DeploymentCreateResponse|error deploymentResponse = entity:createDeployment(userInfo.idToken,
                {
                    projectId: id,
                    name: payload.name,
                    description: payload.description,
                    typeKey: payload.deploymentTypeKey
                });
        if deploymentResponse is error {
            if getStatusCode(deploymentResponse) == http:STATUS_FORBIDDEN {
                logForbiddenProjectAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            if getStatusCode(deploymentResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            string customError = "Failed to create deployment for the project.";
            log:printError(customError, deploymentResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return deploymentResponse.deployment;
    }

    # Update an existing deployment for a project.
    #
    # + projectId - ID of the project
    # + deploymentId - ID of the deployment to be updated
    # + payload - Deployment update payload
    # + return - Updated deployment or error response
    resource function patch projects/[string projectId]/deployments/[string deploymentId](http:RequestContext ctx,
            entity:DeploymentUpdatePayload payload)
            returns entity:UpdatedDeployment|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        string? validateDeploymentUpdatePayload = entity:validateDeploymentUpdatePayload(payload);
        if validateDeploymentUpdatePayload is string {
            log:printWarn(validateDeploymentUpdatePayload);
            return <http:BadRequest>{
                body: {
                    message: validateDeploymentUpdatePayload
                }
            };
        }

        entity:DeploymentUpdateResponse|error response = entity:updateDeployment(userInfo.idToken,
                deploymentId, payload);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to update deployment: ${
                        deploymentId} for project: ${projectId}`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to update the deployment for the selected project."
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            string customError = "Failed to update the deployment.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response.deployment;
    }

    # Get overall project statistics by ID.
    #
    # + id - ID of the project
    # + return - Project statistics response or error
    resource function get projects/[string id]/stats(http:RequestContext ctx, string[]? caseTypes)
        returns types:ProjectStatsResponse|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        // Verify project access
        entity:ProjectResponse|error projectResponse = entity:getProject(userInfo.idToken, id);
        if projectResponse is error {
            if getStatusCode(projectResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(projectResponse) == http:STATUS_FORBIDDEN {
                logForbiddenProjectAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            log:printError(ERR_MSG_FETCHING_PROJECT_DETAILS, projectResponse);
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_FETCHING_PROJECT_DETAILS
                }
            };
        }

        // Fetch case stats
        entity:ProjectCaseStatsResponse|error caseStats =
            entity:getCaseStatsForProject(userInfo.idToken, id, caseTypes);
        if caseStats is error {
            log:printError(ERR_MSG_CASES_STATISTICS, caseStats);
            // To return other stats even if case stats retrieval fails, error will not be returned.
        }

        // Fetch chat stats
        entity:ProjectChatStatsResponse|error chatStats = entity:getChatStatsForProject(userInfo.idToken, id);
        if chatStats is error {
            log:printError(ERR_MSG_CHATS_STATISTICS, chatStats);
            // To return other stats even if chat stats retrieval fails, error will not be returned.
        }

        // Fetch deployment stats
        entity:ProjectDeploymentStatsResponse|error deploymentStats =
            entity:getDeploymentStatsForProject(userInfo.idToken, id);
        if deploymentStats is error {
            log:printError("Failed to retrieve project deployment statistics.", deploymentStats);
            // To return other stats even if deployment stats retrieval fails, error will not be returned.
        }

        // Fetch project activity stats
        entity:ProjectStatsResponse|error projectActivityStats = entity:getProjectActivityStats(userInfo.idToken, id);
        if projectActivityStats is error {
            log:printError("Failed to retrieve project activity statistics.", projectActivityStats);
            // To return other stats even if project activity stats retrieval fails, error will not be returned.
        }

        return {
            projectStats: {
                openCases: caseStats is entity:ProjectCaseStatsResponse ?
                    getOpenCasesCountFromProjectCasesStats(caseStats) : (),
                activeChats: chatStats is entity:ProjectChatStatsResponse ? chatStats.activeCount : (),
                deployments: deploymentStats is entity:ProjectDeploymentStatsResponse ? deploymentStats.totalCount : (),
                slaStatus: projectActivityStats is entity:ProjectStatsResponse ? projectActivityStats.slaStatus : ()
            },
            recentActivity: {
                totalTimeLogged:
                    projectActivityStats is entity:ProjectStatsResponse ? projectActivityStats.totalTimeLogged : (),
                billableHours:
                    projectActivityStats is entity:ProjectStatsResponse ? projectActivityStats.billableHours : (),
                lastDeploymentOn:
                    deploymentStats is entity:ProjectDeploymentStatsResponse ? deploymentStats.lastDeploymentOn : ()
            }
        };
    }

    # Get cases statistics for a project by ID.
    #
    # + id - ID of the project
    # + return - Project statistics overview or error response
    resource function get projects/[string id]/stats/cases(http:RequestContext ctx, string[]? caseTypes)
        returns types:ProjectCaseStats|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        // Verify project access
        entity:ProjectResponse|error projectResponse = entity:getProject(userInfo.idToken, id);
        if projectResponse is error {
            if getStatusCode(projectResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(projectResponse) == http:STATUS_FORBIDDEN {
                logForbiddenProjectAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            log:printError(ERR_MSG_FETCHING_PROJECT_DETAILS, projectResponse);
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_FETCHING_PROJECT_DETAILS
                }
            };
        }

        entity:ProjectCaseStatsResponse|error caseStats =
            entity:getCaseStatsForProject(userInfo.idToken, id, caseTypes);
        if caseStats is error {
            log:printError(ERR_MSG_CASES_STATISTICS, caseStats);
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_CASES_STATISTICS
                }
            };
        }

        return mapCaseStats(caseStats);
    }

    # Get project support statistics by ID.
    #
    # + id - ID of the project
    # + return - Project support statistics or error response
    resource function get projects/[string id]/stats/support(http:RequestContext ctx, string[]? caseTypes)
        returns types:ProjectSupportStats|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        // Verify project access
        entity:ProjectResponse|error projectResponse = entity:getProject(userInfo.idToken, id);
        if projectResponse is error {
            if getStatusCode(projectResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(projectResponse) == http:STATUS_FORBIDDEN {
                logForbiddenProjectAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_PROJECT_ACCESS_FORBIDDEN
                    }
                };
            }

            log:printError(ERR_MSG_FETCHING_PROJECT_DETAILS, projectResponse);
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_FETCHING_PROJECT_DETAILS
                }
            };
        }

        // Fetch case stats 
        entity:ProjectCaseStatsResponse|error caseStats =
            entity:getCaseStatsForProject(userInfo.idToken, id, caseTypes);
        if caseStats is error {
            log:printError(ERR_MSG_CASES_STATISTICS, caseStats);
            // To return other stats even if case stats retrieval fails, error will not be returned.
        }

        // Fetch chat stats
        entity:ProjectChatStatsResponse|error chatStats = entity:getChatStatsForProject(userInfo.idToken, id);
        if chatStats is error {
            log:printError(ERR_MSG_CHATS_STATISTICS, chatStats);
            // To return other stats even if chat stats retrieval fails, error will not be returned.
        }

        return {
            totalCases: caseStats is entity:ProjectCaseStatsResponse ? caseStats.totalCount : (),
            activeChats: chatStats is entity:ProjectChatStatsResponse ? chatStats.activeCount : (),
            sessionChats: chatStats is entity:ProjectChatStatsResponse ? chatStats.sessionCount : (),
            resolvedChats: chatStats is entity:ProjectChatStatsResponse ? chatStats.resolvedCount : ()
        };
    }

    # Get case details by ID.
    #
    # + id - ID of the case
    # + return - Case details or error
    resource function get cases/[string id](http:RequestContext ctx)
        returns entity:CaseResponse|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:CaseResponse|error caseResponse = entity:getCase(userInfo.idToken, id);
        if caseResponse is error {
            if getStatusCode(caseResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(caseResponse) == http:STATUS_FORBIDDEN {
                logForbiddenCaseAccess(id, userInfo.userId);
                return <http:Forbidden>{
                    body: {
                        message: ERR_MSG_CASE_ACCESS_FORBIDDEN
                    }
                };
            }

            string customError = "Failed to retrieve case details.";
            log:printError(customError, caseResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return caseResponse;
    }

    # Create a new case.
    #
    # + payload - Case creation payload
    # + return - Success message or error response
    resource function post cases(http:RequestContext ctx, entity:CaseCreatePayload payload)
        returns http:Created|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:CaseCreateResponse|error createdCaseResponse = entity:createCase(userInfo.idToken, payload);
        if createdCaseResponse is error {
            if getStatusCode(createdCaseResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(createdCaseResponse) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to create a case for project: ${
                        payload.projectId}!`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to create a case for the selected project. " +
                        "Please check your access permissions or contact support."
                    }
                };
            }

            string customError = "Failed to create a new case.";
            log:printError(customError, createdCaseResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return <http:Created>{
            body: mapCreatedCase(createdCaseResponse.case)
        };
    }

    # Search cases for a specific project with filters and pagination.
    #
    # + id - ID of the project
    # + payload - Case search request body
    # + return - Paginated cases or error
    resource function post projects/[string id]/cases/search(http:RequestContext ctx, types:CaseSearchPayload payload)
        returns http:Ok|http:BadRequest|http:Unauthorized|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        types:CaseSearchResponse|error casesResponse = searchCases(userInfo.idToken, id, payload);
        if casesResponse is error {
            if getStatusCode(casesResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            string customError = "Failed to retrieve cases.";
            log:printError(customError, casesResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return <http:Ok>{
            body: casesResponse
        };
    }

    # Get case filters for a project.
    #
    # + id - ID of the project
    # + return - Case filters or error
    resource function get projects/[string id]/filters(http:RequestContext ctx)
        returns types:ProjectFilterOptions|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:ProjectMetadataResponse|error projectMetadata = entity:getProjectMetadata(userInfo.idToken, id);
        if projectMetadata is error {
            if getStatusCode(projectMetadata) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(projectMetadata) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to access project filters for project: ${
                        id}`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to access the filters for the selected project."
                    }
                };
            }

            string customError = "Failed to retrieve project filters.";
            log:printError(customError, projectMetadata);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return getProjectFilters(projectMetadata);
    }

    # Classify the case using AI chat agent.
    #
    # + payload - Case classification payload
    # + return - Case classification response or an error
    resource function post cases/classify(http:RequestContext ctx, ai_chat_agent:CaseClassificationPayload payload)
        returns ai_chat_agent:CaseClassificationResponse|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        // TODO: Need to persist the chat history
        ai_chat_agent:CaseClassificationResponse|error classificationResponse =
            ai_chat_agent:createCaseClassification(payload);
        if classificationResponse is error {
            string customError = "Failed to classify chat message.";
            log:printError(customError, classificationResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return classificationResponse;
    }

    # Get comments for a specific case.
    #
    # + id - ID of the case
    # + limit - Number of comments to retrieve
    # + offset - Offset for pagination
    # + return - Comments response or error
    resource function get cases/[string id]/comments(http:RequestContext ctx, int? 'limit, int? offset)
        returns types:CommentsResponse|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        if isInvalidLimitOffset('limit, offset) {
            return <http:BadRequest>{
                body: {
                    message: ERR_LIMIT_OFFSET_INVALID
                }
            };
        }

        entity:CommentsResponse|error commentsResponse = entity:getComments(userInfo.idToken, id, 'limit, offset);
        if commentsResponse is error {
            string customError = "Failed to retrieve comments.";
            log:printError(customError, commentsResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return mapCommentsResponse(commentsResponse);
    }

    # Get attachments for a specific case.
    #
    # + id - ID of the case
    # + limit - Number of attachments to retrieve
    # + offset - Offset for pagination
    # + return - Attachments response or error
    resource function get cases/[string id]/attachments(http:RequestContext ctx, int? 'limit, int? offset)
        returns types:AttachmentsResponse|http:BadRequest|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        if isInvalidLimitOffset('limit, offset) {
            return <http:BadRequest>{
                body: {
                    message: ERR_LIMIT_OFFSET_INVALID
                }
            };
        }

        entity:AttachmentsResponse|error attachmentResponse =
            entity:getAttachments(userInfo.idToken, id, 'limit, offset);
        if attachmentResponse is error {
            string customError = "Failed to retrieve attachments.";
            log:printError(customError, attachmentResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return mapAttachmentsResponse(attachmentResponse);
    }

    # Create a new comment for a specific case.
    #
    # + id - ID of the case
    # + payload - Comment creation payload
    # + return - Created comment or error response
    resource function post cases/[string id]/comments(http:RequestContext ctx, types:CommentCreatePayload payload)
        returns entity:CreatedComment|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:CommentCreateResponse|error createdCaseResponse = entity:createComment(userInfo.idToken,
                {
                    referenceId: id,
                    referenceType: entity:CASE,
                    content: payload.content,
                    'type: payload.'type
                });
        if createdCaseResponse is error {
            if getStatusCode(createdCaseResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(createdCaseResponse) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to comment on case with ID: ${id}!`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to comment on the requested case. " +
                        "Please check your access permissions or contact support."
                    }
                };
            }

            string customError = "Failed to create a new comment.";
            log:printError(customError, createdCaseResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return createdCaseResponse.comment;
    }

    # Create a new attachment for a specific case.
    #
    # + id - ID of the case
    # + return - Created attachment or error response
    resource function post cases/[string id]/attachments(http:RequestContext ctx, types:AttachmentPayload payload)
        returns types:CreatedAttachment|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:AttachmentCreateResponse|error createdAttachmentResponse = entity:createAttachment(userInfo.idToken,
                {
                    referenceId: id,
                    referenceType: entity:CASE,
                    name: payload.name,
                    'type: payload.'type,
                    file: payload.content
                });
        if createdAttachmentResponse is error {
            if getStatusCode(createdAttachmentResponse) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(createdAttachmentResponse) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to add attachment to case with ID: ${id}!`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to add attachments to the requested case. " +
                        "Please check your access permissions or contact support."
                    }
                };
            }

            string customError = "Failed to create a new attachment.";
            log:printError(customError, createdAttachmentResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return {
            id: createdAttachmentResponse.attachment.id,
            size: createdAttachmentResponse.attachment.sizeBytes,
            createdOn: createdAttachmentResponse.attachment.createdOn,
            createdBy: createdAttachmentResponse.attachment.createdBy,
            downloadUrl: createdAttachmentResponse.attachment.downloadUrl
        };
    }

    # Get products of a deployment by deployment ID.
    #
    # + id - ID of the deployment
    # + return - Deployed products response or error response
    resource function get deployments/[string id]/products(http:RequestContext ctx)
        returns types:DeployedProduct[]|http:BadRequest|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:DeployedProductsResponse|error productsResponse = entity:getDeployedProducts(userInfo.idToken, id);
        if productsResponse is error {
            if getStatusCode(productsResponse) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to deployment ID: ${id} is forbidden for user: ${userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to the requested deployment is forbidden!"
                    }
                };
            }

            string customError = "Failed to retrieve products for the deployment.";
            log:printError(customError, productsResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return mapDeployedProducts(productsResponse);
    }

    # Add a product to a deployment by deployment ID.
    #
    # + id - ID of the deployment
    # + payload - Deployed product creation payload
    # + return - Created deployed product or error response
    resource function post deployments/[string id]/products(http:RequestContext ctx,
            types:DeployedProductCreatePayload payload) returns
        entity:CreatedDeployedProduct|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:DeployedProductCreateResponse|error response = entity:createDeployedProduct(userInfo.idToken,
                {
                    deploymentId: id,
                    productId: payload.productId,
                    projectId: payload.projectId,
                    versionId: payload.versionId,
                    cores: payload?.cores,
                    tps: payload?.tps
                });
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to add product to deployment with ID: ${
                        id}!`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to add products to the deployment. " +
                        "Please check your access permissions or contact support."
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_BAD_REQUEST {
                string customError = "Invalid request parameters for adding product to the deployment.";
                log:printWarn(customError, response);
                return <http:BadRequest>{
                    body: {
                        message: customError
                    }
                };
            }
            string customError = "Failed to add product to the deployment.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response.deployedProduct;
    }

    # Update a product in a deployment by deployment ID and product ID.
    #
    # + deploymentId - ID of the deployment
    # + productId - ID of the product to be updated
    # + payload - Deployed product update payload
    # + return - Updated deployed product or error response
    resource function patch deployments/[string deploymentId]/products/[string productId](http:RequestContext ctx,
            entity:DeployedProductUpdatePayload payload) returns
        entity:UpdatedDeployedProduct|http:BadRequest|http:Unauthorized|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:DeployedProductUpdateResponse|error response =
            entity:updateDeployedProduct(userInfo.idToken, productId, payload);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `User: ${userInfo.userId} is forbidden to update product with ID: ${
                        productId} in deployment with ID: ${deploymentId}!`);
                return <http:Forbidden>{
                    body: {
                        message: "You're not authorized to update products in the deployment. " +
                        "Please check your access permissions or contact support."
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_UNAUTHORIZED {
                log:printWarn(string `User: ${userInfo.userId} is not authorized to access the customer portal!`);
                return <http:Unauthorized>{
                    body: {
                        message: ERR_MSG_UNAUTHORIZED_ACCESS
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_BAD_REQUEST {
                string customError = "Invalid request parameters for updating product in the deployment.";
                log:printWarn(customError, response);
                return <http:BadRequest>{
                    body: {
                        message: customError
                    }
                };
            }

            string customError = "Failed to update product in the deployment.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response.deployedProduct;
    }

    # Get recommended update levels.
    #
    # + return - List of recommended update levels or an error
    resource function get updates/recommended\-update\-levels(http:RequestContext ctx)
        returns types:RecommendedUpdateLevel[]|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        types:RecommendedUpdateLevel[]|error recommendedUpdateLevels =
            updates:processRecommendedUpdateLevels(userInfo.email);
        if recommendedUpdateLevels is error {
            string customError = "Failed to retrieve recommended update levels.";
            log:printError(customError, recommendedUpdateLevels);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return recommendedUpdateLevels;
    }

    # Search updates based on provided filters.
    #
    # + payload - Update search payload containing filters
    # + return - List of updates matching or an error
    resource function post updates/search(http:RequestContext ctx, types:ListUpdatePayload payload)
        returns http:Ok|http:BadRequest|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        types:UpdateResponse|error updateResponse = updates:processListUpdates(payload);
        if updateResponse is error {
            string customError = "Failed to search updates.";
            log:printError(customError, updateResponse);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return <http:Ok>{
            body: updateResponse
        };
    }

    # Get product update levels.
    #
    # + return - List of product update levels or an error
    resource function get updates/product\-update\-levels(http:RequestContext ctx)
        returns types:ProductUpdateLevel[]|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        types:ProductUpdateLevel[]|error productUpdateLevels = updates:processProductUpdateLevels();
        if productUpdateLevels is error {
            string customError = "Failed to get product update levels.";
            log:printError(customError, productUpdateLevels);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return productUpdateLevels;
    }

    # Get products.
    #
    # + return - List of products or an error
    resource function get products(http:RequestContext ctx, int? offset, int? 'limit) returns entity:ProductsResponse|http:InternalServerError {
        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:ProductsResponse|error response = entity:getProducts(userInfo.idToken, {}); // TODO: Handle pagination
        if response is error {
            string customError = "Failed to retrieve products.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response;
    }

    # Search product vulnerabilities based on provided filters.
    #
    # + payload - Product vulnerability search payload containing filters
    # + return - List of product vulnerabilities or an error
    resource function post products/vulnerabilities/search(http:RequestContext ctx,
            entity:ProductVulnerabilitySearchPayload payload) returns http:Ok|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:ProductVulnerabilitySearchResponse|error response =
            entity:searchProductVulnerabilities(userInfo.idToken, payload);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to product vulnerabilities information is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to product vulnerabilities information is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to search product vulnerabilities.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return <http:Ok>{
            body: mapProductVulnerabilitySearchResponse(response)
        };
    }

    # Get product vulnerability by ID.
    #
    # + id - ID of the product vulnerability
    # + return - Product vulnerability details or error
    resource function get products/vulnerabilities/[string id](http:RequestContext ctx)
            returns types:ProductVulnerabilityResponse|http:Forbidden|http:NotFound|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:ProductVulnerabilityResponse|error response = entity:getProductVulnerability(userInfo.idToken, id);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to product vulnerability information is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to product vulnerability information is forbidden for the user!"
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_NOT_FOUND {
                log:printWarn(string `Requested product vulnerability is not found for the user: ${userInfo.userId}`);
                return <http:NotFound>{
                    body: {
                        message: "Requested product vulnerability is not found!"
                    }
                };
            }

            string customError = "Failed to retrieve product vulnerability details.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return mapProductVulnerabilityResponse(response);
    }

    # Get product vulnerability metadata.
    #
    # + return - Product vulnerability metadata or error
    resource function get products/vulnerabilities/meta(http:RequestContext ctx)
        returns types:ProductVulnerabilityMetaResponse|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:VulnerabilityMetaResponse|error response = entity:getProductVulnerabilityMetaData(userInfo.idToken);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to product vulnerability information is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to product vulnerability information is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to retrieve product vulnerability metadata.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        return mapProductVulnerabilityMetadataResponse(response);
    }

    # Get contacts of a project by project ID.
    #
    # + id - ID of the project
    # + return - List of project contacts or error
    resource function get projects/[string id]/contacts(http:RequestContext ctx)
        returns user_management:Contact[]|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        //TODO: Do the project validation

        user_management:Contact[]|error response = user_management:getProjectContacts(id);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to project contacts are forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to project contacts is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to retrieve project contacts.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response;
    }

    # Add a contact to a project by project ID.
    #
    # + id - ID of the project
    # + payload - Contact information to be added
    # + return - Membership information or error response
    resource function post projects/[string id]/contacts(http:RequestContext ctx,
            types:OnBoardContactPayload payload)
        returns user_management:Membership|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        //TODO: Do the project validation

        user_management:Membership|error response = user_management:createProjectContact(id,
                {
                    contactEmail: payload.contactEmail,
                    adminEmail: userInfo.email,
                    contactFirstName: payload.contactFirstName,
                    contactLastName: payload.contactLastName,
                    isCsIntegrationUser: payload.isCsIntegrationUser,
                    isSecurityContact: payload.isSecurityContact

                });
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to add project contact is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to add project contact is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to add project contact.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response;
    }

    # Remove a contact from a project by project ID and contact email.
    #
    # + id - ID of the project
    # + email - Email of the contact to be removed
    # + return - Membership information or error response
    resource function delete projects/[string id]/contacts/[string email](http:RequestContext ctx)
        returns http:Ok|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        //TODO: Do the project validation

        user_management:Membership|error response = user_management:removeProjectContact(id, email, userInfo.email);
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to remove project contact is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to remove project contact is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to remove project contact.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return <http:Ok>{
            body: {
                message: "Project contact removed successfully!"
            }
        };
    }

    # Update a contact's role in a project by project ID and contact email.
    #
    # + id - ID of the project
    # + email - Email of the contact whose role is to be updated
    # + payload - Updated role information
    # + return - Membership information or error response
    resource function patch projects/[string id]/contacts/[string email](http:RequestContext ctx,
            types:MembershipSecurityPayload payload)
        returns user_management:Membership|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        //TODO: Do the project validation

        user_management:Membership|error response = user_management:updateMembershipFlag(id, email,
                {
                    adminEmail: userInfo.email,
                    isSecurityContact: payload.isSecurityContact
                });
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to update project contact is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to update project contact is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to update project contact.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response;
    }

    # Validate if a contact can be added to a project by project ID.
    #
    # + id - ID of the project
    # + payload - Contact information to be validated
    # + return - Contact information if valid or error response
    resource function post projects/[string id]/contacts/validate(http:RequestContext ctx,
            types:ValidationPayload payload)
        returns user_management:Contact|http:BadRequest|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        //TODO: Do the project validation

        user_management:Contact|error? response = user_management:validateProjectContact(
                {
                    contactEmail: payload.contactEmail,
                    adminEmail: userInfo.email,
                    projectId: id
                });
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to validate project contact is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to validate project contact is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to validate project contact.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        if response is () {
            string customError = "The provided information is not valid.";
            log:printWarn(customError);
            return <http:BadRequest>{
                body: {
                    message: customError
                }
            };
        }
        return response;
    }

    # Search call requests for a specific case with filters and pagination.
    #
    # + id - ID of the case
    # + payload - Call request search payload containing filters and pagination info
    # + return - List of call requests matching the criteria or an error
    resource function post cases/[string id]/call\-requests/search(http:RequestContext ctx,
            types:CallRequestSearchPayload payload)
        returns http:Ok|http:BadRequest|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        entity:CallRequestsResponse|error response = entity:searchCallRequests(userInfo.idToken,
                {
                    caseId: id,
                    filters: payload.filters,
                    pagination: payload.pagination
                });
        if response is error {
            if getStatusCode(response) == http:STATUS_BAD_REQUEST {
                return <http:BadRequest>{
                    body: {
                        message: "Invalid request parameters for searching call requests."
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to call request information is forbidden for user: ${
                        userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to call request information is forbidden for the user!"
                    }
                };
            }

            string customError = "Failed to search call requests.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return <http:Ok>{
            body: mapSearchCallRequestResponse(response)
        };
    }

    # Create a call request for a specific case.
    #
    # + id - ID of the case
    # + payload - Call request creation payload
    # + return - Created call request details or an error
    resource function post cases/[string id]/call\-requests(http:RequestContext ctx,
            types:CallRequestCreatePayload payload)
        returns entity:CreatedCallRequest|http:BadRequest|http:Forbidden|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        string|error? validateUtcTimesError = entity:validateUtcTimes(payload.utcTimes);
        if validateUtcTimesError is string {
            log:printWarn(validateUtcTimesError);
            return <http:BadRequest>{
                body: {
                    message: validateUtcTimesError
                }
            };
        }

        if validateUtcTimesError is error {
            string customError = "Failed to validate UTC times for call request creation.";
            log:printError(customError, validateUtcTimesError);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        entity:CallRequestCreateResponse|error response = entity:createCallRequest(userInfo.idToken,
                {
                    caseId: id,
                    reason: payload.reason,
                    utcTimes: payload.utcTimes,
                    durationInMinutes: payload.durationInMinutes
                });
        if response is error {
            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to create call request is forbidden for the user: ${userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to create call request is forbidden for the user!"
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_BAD_REQUEST {
                return <http:BadRequest>{
                    body: {
                        message: "Invalid request parameters for creating a call request."
                    }
                };
            }

            string customError = "Failed to create call request.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response.callRequest;
    }

    # Update a call request for a specific case.
    #
    # + caseId - ID of the case
    # + callRequestId - ID of the call request
    # + payload - Call request update payload
    # + return - Updated call request details or an error
    resource function patch cases/[string caseId]/call\-requests/[string callRequestId](http:RequestContext ctx,
            types:CallRequestUpdatePayload payload)
        returns entity:UpdatedCallRequest|http:BadRequest|http:Forbidden|http:NotFound|http:InternalServerError {

        authorization:UserInfoPayload|error userInfo = ctx.getWithType(authorization:HEADER_USER_INFO);
        if userInfo is error {
            return <http:InternalServerError>{
                body: {
                    message: ERR_MSG_USER_INFO_HEADER_NOT_FOUND
                }
            };
        }

        string? validationError = entity:validateCallRequestUpdatePayload(payload);
        if validationError is string {
            log:printWarn(validationError);
            return <http:BadRequest>{
                body: {
                    message: validationError
                }
            };
        }

        string|error? validateUtcTimesError = entity:validateUtcTimes(payload.utcTimes);
        if validateUtcTimesError is string {
            log:printWarn(validateUtcTimesError);
            return <http:BadRequest>{
                body: {
                    message: validateUtcTimesError
                }
            };
        }

        if validateUtcTimesError is error {
            string customError = "Failed to validate UTC times for call request update.";
            log:printError(customError, validateUtcTimesError);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }

        string? validateCallRequestUpdatePayload = entity:validateCallRequestUpdatePayload(payload);
        if validateCallRequestUpdatePayload is string {
            log:printWarn(validateCallRequestUpdatePayload);
            return <http:BadRequest>{
                body: {
                    message: validateCallRequestUpdatePayload
                }
            };
        }

        entity:CallRequestUpdateResponse|error response = entity:updateCallRequest(userInfo.idToken, callRequestId,
                payload);
        if response is error {
            if getStatusCode(response) == http:STATUS_BAD_REQUEST {
                return <http:BadRequest>{
                    body: {
                        message: "Invalid request parameters for updating call request."
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_FORBIDDEN {
                log:printWarn(string `Access to update call request is forbidden for user: ${userInfo.userId}`);
                return <http:Forbidden>{
                    body: {
                        message: "Access to update call request is forbidden for the user!"
                    }
                };
            }

            if getStatusCode(response) == http:STATUS_NOT_FOUND {
                return <http:NotFound>{
                    body: {
                        message: "The call request to be updated is not found!"
                    }
                };
            }

            string customError = "Failed to update call request.";
            log:printError(customError, response);
            return <http:InternalServerError>{
                body: {
                    message: customError
                }
            };
        }
        return response.callRequest;
    }
}
